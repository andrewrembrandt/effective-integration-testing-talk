plugins {
    id 'org.springframework.boot' version '3.2.2'
    id 'io.spring.dependency-management' version '1.1.4'
    id "org.owasp.dependencycheck" version '7.1.0.1'
    id 'java'
    id "com.github.ben-manes.versions" version '0.42.0'
}

group = 'ch.andrewrembrandt.effectiveit'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '21'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
    maven { url 'https://repo.spring.io/snapshot' }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
    runtimeOnly 'org.postgresql:r2dbc-postgresql'

    implementation 'org.mapstruct:mapstruct:1.5.3.Final'
    implementation 'org.projectlombok:lombok'
    implementation 'org.springdoc:springdoc-openapi-webflux-ui:1.6.15'

    implementation 'net.bytebuddy:byte-buddy:1.14.2'
    implementation 'org.reflections:reflections:0.10.2'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.3.Final'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'io.projectreactor:reactor-test:3.5.4'
    testImplementation 'pl.rzrz:assertj-reactor:1.0.8'

    testImplementation 'org.testcontainers:testcontainers'
    testRuntimeOnly 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'org.testcontainers:r2dbc'

    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.mapstruct:mapstruct-processor:1.5.3.Final'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    developmentOnly 'io.projectreactor:reactor-tools'
}

test {
    useJUnitPlatform()
    testLogging {
        events "skipped", "failed"
        exceptionFormat "full"
        showCauses true
        showStackTraces true
//        showStandardStreams = true
    }
}

task startPostgres(type: Exec) {
    commandLine './postgresql-start.sh'
}

bootRun.dependsOn startPostgres

dependencyCheck {
    suppressionFile './resources/cve-suppression.xml'
}

compileJava {
    options.compilerArgs += [
            '-Amapstruct.verbose=true'
    ]
}
